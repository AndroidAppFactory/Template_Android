apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
// 不同类型的包，仅仅修改下面两项即可，其余内容保持一致
project.ext.applicationID = "com.bihe0832.android.template.android"
project.ext.applicationPrefix = "ZTAndroid"
project.ext.routerSchema = "ztandroid"
project.ext.applicationName = project.ext.applicationPrefix + "Demo"
project.ext.test_activity_name = project.ext.applicationPrefix + "调试"

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        applicationId applicationID
        minSdkVersion project.appMinSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionName project.versionName
        versionCode project.versionCode
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

    }

    packagingOptions {
        project.ndk_abiFilters_project.split(",").each { abiItem ->
            def finalDoNotStripFileName = "*/" + abiItem + "/*.so"
            doNotStrip finalDoNotStripFileName
        }

        project.ndk_abiFilters_all.split(",").each { abiItem ->
            project.pickFirstList.each { item ->
                def finalPickFirstFileName = "lib/" + abiItem + "/" + item
                pickFirst finalPickFirstFileName
            }
        }

        project.excludeList.each { item ->
            exclude item
        }
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            keyAlias project.signingConfigs.keyAlias
            keyPassword project.signingConfigs.keyPassword
            storeFile file(project.signingConfigs.storeFile)
            storePassword project.signingConfigs.storePassword
            v1SigningEnabled project.signingConfigs.v1SigningEnabled as boolean
            v2SigningEnabled project.signingConfigs.v2SigningEnabled as boolean
        }

        release {
            keyAlias project.signingConfigs.keyAlias
            keyPassword project.signingConfigs.keyPassword
            storeFile file(project.signingConfigs.storeFile)
            storePassword project.signingConfigs.storePassword
            v1SigningEnabled project.signingConfigs.v1SigningEnabled as boolean
            v2SigningEnabled project.signingConfigs.v2SigningEnabled as boolean
        }
    }


    buildTypes {
        debug {
            resValue "string", "app_name", applicationName
            resValue "string", "router_schema", routerSchema
            resValue "string", "test_activity_name", test_activity_name

            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
        }

        release {
            resValue "string", "app_name", applicationName
            resValue "string", "router_schema", routerSchema
            resValue "string", "test_activity_name", test_activity_name

            signingConfig signingConfigs.release
            minifyEnabled project.minifyEnabled as boolean
            proguardFiles "./../proguard-rules-common.pro"
            shrinkResources false
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = project.compose_version
    }

    android.applicationVariants.all { variant ->
        def finalFileName = applicationPrefix + "_V${defaultConfig.versionName}_${defaultConfig.versionCode}"
        variant.outputs.all {
            if (variant.buildType.name == 'debug') {
                outputFileName = finalFileName + "_debug.apk"
            } else {
                outputFileName = finalFileName + "_release.apk"
            }
        }
    }
    
    configurations.all {
        resolutionStrategy {
            force("androidx.lifecycle:lifecycle-livedata-core:2.6.0")
        }
    }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    api project(':Application')
    api "com.bihe0832.android:common-compose-debug:8.0.6"
    kapt "com.bihe0832.android:lib-router-compiler:5.1.7"
}